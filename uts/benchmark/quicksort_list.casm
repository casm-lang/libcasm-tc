//  
//  Copyright (c) 2014-2016 CASM Organization
//  All rights reserved.
//  
//  Developed by: Philipp Paulweber
//                Emmanuel Pescosta
//                https://github.com/casm-lang/libcasm-tc
//  
//  This file is part of libcasm-tc.
//  
//  libcasm-tc is free software: you can redistribute it and/or modify
//  it under the terms of the GNU General Public License as published by
//  the Free Software Foundation, either version 3 of the License, or
//  (at your option) any later version.
//  
//  libcasm-tc is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  GNU General Public License for more details.
//  
//  You should have received a copy of the GNU General Public License
//  along with libcasm-tc. If not, see <http://www.gnu.org/licenses/>.
//  

// TC()
// BM()

CASM quicksort init foo

function stack : -> List(Tuple(Int, Int)) //initially { [] }
function array : Int -> Int //initially { 0->9, 1->2, 2->10, 3->7, 4->8, 5->1, 6->6, 7->3, 8->4, 9->5}
function p : -> Int //initially { undef }
function pivot : -> Int
function ll : -> Int
function rr : -> Int
function l : -> Int
function r : -> Int
function need_pop : -> Boolean
function need_partition : -> Boolean

rule partition_one_step =
seqblock
    iterate
        if array(ll) < array(pivot) then ll := ll + 1
    iterate 
        if array(rr) >= array(pivot) and ll < rr then rr := rr - 1
    if ll < rr then {
        array(ll) := array(rr)
        array(rr) := array(ll)
    } else 
        need_partition := false
endseqblock

rule partition =
if pivot = undef then {
    pivot := r
    rr := r - 1
    ll := l
    need_partition := true
} else
    if need_partition then
        call partition_one_step
    else {
        p := ll
        if pivot != ll then {
            array(pivot) := array(ll)
            array(ll) := array(pivot)
        }
    }

rule quicksort_once =
    if p = undef then
        call partition
    else    
    seqblock
        if l < p-1 then 
            push [l,p-1] into stack
        if p+1 < r then 
            push [p+1,r] into stack
        need_pop := true
    endseqblock

rule quicksort =
    if need_pop then {
        let top = nth(stack, 1) in
            if top != undef then {
                stack := tail(stack)
                l := nth(top, 1)
                r := nth(top, 2)
                pivot := undef
                p := undef
                need_pop := false
            } else {
                forall i in [0..29] do {
                    assert array(i) <= array(i+1)
                    print array(i)
                }
                print "----------------"
                //program(self) := @initR
                program(self) := undef
            }
    }
    else
        call quicksort_once

init initR

rule initR = {
    forall i in [0..30] do {
        array(i) := 30 - i
    }
    p := undef
    {|
        stack := []
        push [0,30] into stack
    |}
    need_pop := true
    program(self) := @quicksort
}

